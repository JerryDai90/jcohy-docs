configurations {
    asciidoctorExt
}

dependencies {
    asciidoctorExt("io.spring.asciidoctor:spring-asciidoctor-extensions:0.2.0.RELEASE")
}

ext{
    language = "zh-cn"
    component = "spring-framework"
}

task extractDocResources(type: Copy) {
    from "../spring-docs-build/src/main/resources"
    into "$buildDir/docs/spring-docs-resources"
}

asciidoctorj {
    modules {
        pdf {
            version '1.5.0-beta.8'
        }
    }
}

asciidoctor {
    baseDir "${sourceDir}/${language}/${component}"
    configurations 'asciidoctorExt'
    sources {
        include "${language}/${component}/*.adoc"
    }
    outputDir "$buildDir/docs/ref-docs/"
    resources {
        from(sourceDir) {
            include "${language}/${component}/images/*"
        }
    }
    logDocuments = true
    outputOptions {
        backends = ["html5"]
    }

    options doctype: 'book', eruby: 'erubis'
    attributes([
            icons: 'font',
            idprefix: '',
            idseparator: '-',
            docinfo: 'shared',
            revnumber: "${springVersion}",
            sectanchors: '',
            sectnums: '',
            'source-highlighter': 'highlight.js',
            highlightjsdir: "http://document.jcohy.com/js/highlight",
            'highlightjs-theme': 'github',
            stylesdir: "$buildDir/docs/spring-docs-resources/css",
            stylesheet: 'stylesheet.css',
            'spring-version': "${springVersion}"
    ])
}

asciidoctor.dependsOn extractDocResources