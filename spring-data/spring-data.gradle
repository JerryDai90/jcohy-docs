configurations {
    asciidoctorExt
}

dependencies {
    asciidoctorExt("io.spring.asciidoctor:spring-asciidoctor-extensions:0.2.0.RELEASE")
}

ext{
    language = "zh-cn"
    component = "spring-data-jpa"
}

task extractDocResources(type: Copy) {
    from "../spring-docs-build/src/main/resources"
    into "$buildDir/docs/spring-docs-resources/${language}/${component}"
}

asciidoctor {

    sources {
        include "${language}/${component}/*.adoc"
    }
    outputDir "$buildDir/docs/ref-docs/"
    resources {
        from(sourceDir) {
            include "${language}/${component}/images/*"
        }
        from "$buildDir/docs/spring-docs-resources/"
    }
    logDocuments = true
    backends = ["html5"]

    options doctype: 'book', eruby: 'erubis'
    attributes([
            icons: 'font',
            idprefix: '',
            idseparator: '-',
            docinfo: 'shared',
            revnumber: "${springDataJpaVersion}",
            sectanchors: '',
            sectnums: '',
            'source-highlighter': 'highlight.js',
            highlightjsdir: "./js/highlight",
            'highlightjs-theme': 'github',
            stylesdir: './css/',
            stylesheet: 'stylesheet.css',
            'spring-version': "${springVersion}"
    ])
}

asciidoctor.dependsOn extractDocResources